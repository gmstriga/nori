apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "io.github.tjg1.nori"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 12
        versionName "2017.06"
        testApplicationId "io.github.tjg1.nori.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"

        buildConfigField "boolean", "GOOGLE_BUILD", "false"
        buildConfigField "boolean", "DONATIONS_PAYPAL", "true"
        buildConfigField "boolean", "DONATIONS_PATREON", "true"
        buildConfigField "boolean", "DONATIONS_BITCOIN", "true"
        buildConfigField "boolean", "DONATIONS_GOOGLE", "true"

        buildConfigField "String", "DONATIONS_PAYPAL_EMAIL", "null"
        buildConfigField "String", "DONATIONS_PATREON_ACCOUNT", "null"
        buildConfigField "String", "DONATIONS_BITCOIN_ADDRESS", "null"
        buildConfigField "String", "DONATIONS_GOOGLE_PUB_KEY", "null"
        buildConfigField "String", "HOCKEYAPP_APP_ID", "\"73abce41065f421f993f9309d969aaa9\""
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            versionNameSuffix "-${gitRevision()}"
        }
    }
    compileOptions {
        // Enable Java 1.7 features.
        sourceCompatibility JavaVersion.VERSION_1_7;
        targetCompatibility JavaVersion.VERSION_1_7;
    }
    lintOptions {
        abortOnError false
    }
    productFlavors {
        fdroid {
            buildConfigField "boolean", "DONATIONS_GOOGLE", "false"
            buildConfigField "String", "DONATIONS_PAYPAL_EMAIL", "\"tomg@fastmail.uk\""
            buildConfigField "String", "DONATIONS_PATREON_ACCOUNT", "\"3696048\""
            buildConfigField "String", "DONATIONS_BITCOIN_ADDRESS", "\"1NHUsSzj3N7TE8rykUYk8rqqLWWSmTVzdp\""
        }
        google {
            buildConfigField "boolean", "GOOGLE_BUILD", "true"
            buildConfigField "boolean", "DONATIONS_PAYPAL", "false"
            buildConfigField "boolean", "DONATIONS_PATREON", "false"
            buildConfigField "boolean", "DONATIONS_BITCOIN", "false"
            buildConfigField "String", "DONATIONS_GOOGLE_PUB_KEY", "\"MIIBIjANBgkqhkiG9w0BAQEFAAOCAQ8AMIIBCgKCAQEArXl8mF/3pfc+wk6DPaZV8jQoWmw2+/1f5Hc8Rb66Ykv5muiTDEnDtGdaQjfTOHPJrlYGzK3V/KP5zT1X7+kxHzuoATgr0aeOUpu6SQjOKsYlG92yb6ga1+Gj/7zak/gxRcu2ivSxM7TEYeCsxlqfqvdN6EZKRrSy6m5jVqPDwClcQfgWkN+fzb19Jooetb3IrJv73LoB93D82SvndrhuTzMSX4j5ecBMuXVCACGDNYBBZO/B7j+J3/I+p5XmB6bHBH2EqG2BJVXRfvDz424MIW9UpEYvITWZ5XaqOwRKdiBEEkeT/RF/Pe4DnlVArG0TIjKYhPKg3IINQLQ0ZG30dQIDAQAB\""
        }
    }
}

dependencies {
    // Support libraries.
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:design:25.3.1'

    // Third-party libraries.
    // An HTTP+SPDY client and image loader for Android. https://github.com/koush/ion
    compile 'com.koushikdutta.ion:ion:2.1.7'
    // Implementation of ImageView for Android that supports zooming, by various touch gestures.
    compile 'com.github.chrisbanes.photoview:library:1.2.4'
    // Crash reporting library.
    compile 'ch.acra:acra:4.9.2'
    // Utility library for easier unit testing.
    androidTestCompile 'org.easytesting:fest-assert-core:2.0M10'

    // Project dependencies.
    compile project(':norilib') // https://github.com/tjg1/norilib

    // *.jar files.
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

/*
 * This file is part of nori.
 * Copyright (c) 2014-2016 Tomasz Jan GÃ³ralczyk <tomg@fastmail.uk>
 * License: GNU GPLv2
 */

// Used to append Git commit hash to version names.
def gitRevision() {
    def cmd = "git rev-parse --short HEAD"
    return cmd.execute().text.trim()
}
