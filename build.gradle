/*
 * This file is part of nori.
 * Copyright (c) 2014 Tomasz Jan GÃ³ralczyk <tomg@fastmail.uk>
 * License: ISC
 */

apply plugin: 'com.android.library'

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.2.3'
  }
}

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.2"

  defaultConfig {
    minSdkVersion 10
    targetSdkVersion 25
    versionCode 5
    versionName "3.1.1"
    testApplicationId "io.github.tjg1.library.norilib.test"
    testInstrumentationRunner "android.test.InstrumentationTestRunner"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    debug {
      minifyEnabled false
      versionNameSuffix "-${gitRevision()}"
    }
  }
  lintOptions {
    abortOnError false
  }
}

dependencies {
  // HTTP Client library from Koush. (https://github.com/koush/ion)
  compile 'com.koushikdutta.ion:ion:2.1.8'
  // Apache utility library for dealing with Collections.
  compile 'org.apache.commons:commons-collections4:4.1'
  // Android support library.
  compile 'com.android.support:support-v4:25.1.1'
  // FEST for easier unit testing.
  androidTestCompile 'org.easytesting:fest-assert-core:2.0M10'

  compile fileTree(dir: 'libs', include: ['*.jar'])
}

repositories {
  jcenter()
}

// Used to append Git commit hash to version names.
static def gitRevision() {
  def cmd = "git rev-parse --short HEAD"
  return cmd.execute().text.trim()
}
